buildscript {
    repositories {
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
    }
}

plugins {
    id 'maven-publish'
    id 'java'
}
apply plugin: "com.github.johnrengelman.shadow"
//evaluationDependsOn(':version')

group = 'com.github.gregtech-intergalactical' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'tesseract'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

assemble.dependsOn shadowJar

configurations {
    embed
    deobf

    embed.extendsFrom(implementation)
    testCompile.extendsFrom(shadow)
}

shadowJar {
    configurations = [project.configurations.embed]
}

// Deobfuscated jar; development purposes.
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task deobfJar(type: ShadowJar) {
    from sourceSets.main.output
    configurations = [project.configurations.deobf]
    classifier "dev"
}

repositories {
    jcenter()
    maven { url = 'https://files.minecraftforge.net/maven' }
}

dependencies {
    //   Main dependencies:
    // Declare here your dependencies here with:
    // 'shadow' if the jar shouldn't be included in the fat jar,
    // 'implementation' if the jar should be included in the jar
    shadow 'org.apache.commons:commons-collections4:4.4'
    shadow 'it.unimi.dsi:fastutil:8.2.1'

    //   Test dependencies:
    testImplementation 'junit:junit:4.11'

    //   Embedded dependencies:
    // Those will be included in the fat jar.
    // You will likely not edit those dependencies.
    if (rootProject.name == "tesseract") {
        embed project(path: ':forge', configuration: 'archives')
        deobf project(path: ':forge', configuration: 'deobf')
    } else {
        embed project(path: ':tesseract:forge', configuration: 'archives')
        deobf project(path: ':tesseract:forge', configuration: 'deobf')
    }
        
    //embed project(path: ':fabric', configuration: 'archives') TODO coming soon(tm).
}

jar {
    zip64 true
}

// Maven publishing:

static def configurePublishingRepositories(repositoryHandler) {
    if (System.env.MAVEN_URL) {
        repositoryHandler.maven {
            url = System.env.MAVEN_URL

            if (System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD) {
                authentication {
                    basic(BasicAuthentication)
                }

                credentials {
                    username = System.env.USERNAME
                    password = System.env.PASSWORD
                }
            }
        }
    }
}